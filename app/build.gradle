import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.google.gms.google.services)
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

apply plugin: 'com.google.gms.google-services'

configurations {
    firebaseAdmin
}

android {
    namespace 'com.example.testapp'
    compileSdk 35

    defaultConfig {
        applicationId "com.example.testapp"
        minSdk 35
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.recyclerview
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    // Import the BoM for the Firebase platform
    implementation platform(libs.firebase.bom)

    // Add the dependency for the Realtime Database library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation libs.google.firebase.database

    // Add the dependency for the Firebase Authentication library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation libs.google.firebase.auth

    /*
        Add the dependency for the GSON library
        This dependency is used to parse JSON data.
        This is used in the SharedPreferenceUtil.java class to parse the object to and from JSON data.
    */
    implementation libs.gson

    /*
        Add the dependency for the Firebase Admin SDK
        Include firebase-admin as a shadowed dependency
    */
    firebaseAdmin(libs.firebase.admin) {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.google.guava', module: 'listenablefuture'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
    }

    def shadedJar = file('./build/libs/firebase-admin-shaded.jar')
    if (shadedJar.exists()) {
        implementation files(shadedJar)
    } else {
        logger.warn("⚠️ firebase-admin-shaded.jar not found. Skipping inclusion.")
        logger.warn("⚠️ Run the shadowJar task in gradle to generate the shaded jar.")
    }

}


//////////////////////////////
// Shadow Jar Configuration //
//////////////////////////////

// Register ShadowJar tasks for creating a shadow jar with the Firebase Admin SDK
tasks.register("shadow", ShadowJar) {
    group = "shadow" // This makes it show under a "shadow" group in Gradle tasks
    description = "Create a shadow jar"
    archiveBaseName.set("firebase-admin-shaded")
    configurations = [project.configurations.firebaseAdmin]

    relocate 'com.google.firebase', 'com.google.admin_firebase'
    relocate 'com.google.common.util.concurrent', 'shadow.com.google.common.util.concurrent'

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}
